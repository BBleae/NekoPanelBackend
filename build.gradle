//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'studio.baka.neko.nekopanel'
version '1.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
}

dependencies {
    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    compileOnly 'com.github.BBleae:NekoEssentials:0.0.2-fix-publishing-2'
    compileOnly 'com.github.Apisium:Uniporter:1.3.3-SNAPSHOT'
    compileOnly 'io.netty:netty-all:4.1.75.Final'

    implementation 'com.github.Apisium:netty-engine.io:03931aef8c'
    implementation ('io.socket:engine.io-server:5.0.1') {
        exclude group: 'com.google.code.gson'
    }
    implementation ('io.socket:socket.io-server:3.0.2') {
        exclude group: 'org.json'
        exclude group: 'com.squareup.okhttp3'
        exclude group: 'io.socket', module: 'engine.io-server'
        exclude group: 'io.socket', module: 'engine.io-client'
    }

    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
}

shadowJar {
    dependencies {
        relocate 'org.json', 'studio.baka.neko.libs.org.json'
        relocate 'javax.servlet', 'studio.baka.neko.libs.javax.servlet'
        relocate 'io.socket', 'studio.baka.neko.libs.io.socket'
    }

    archiveClassifier.set ''
    minimize()
}
build.dependsOn shadowJar
